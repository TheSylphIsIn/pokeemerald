const LOCALID_DEMIVEE=1
const LOCALID_AMELIA=2
const LOCALID_VINOS=3

mapscripts EltheRoad_MapScripts 
{
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_MAIN_GAME_STATE, 0 {
			clearflag(FLAG_NO_MUSIC_ON_WARP)
			addobjectspriteoffset(LOCALID_AMELIA, 5, 0)
			addobjectspriteoffset(LOCALID_DEMIVEE, 6, 0)
			delay(60)
			delay(60)
			delay(60)
			speakername("???")
			msgbox("Wait…is it alive…?", MSGBOX_AUTOCLOSE)
			fadescreen(FADE_FROM_WHITE)
			delay(60)
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("…yes, I am. Personal space?")
			speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			message("Oh, sorry. I was thinking about\n" "eating you, but I guess I won't now.")
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			waitmessage
			waitbuttonpress
			speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
			msgbox("Dude! Seriously?")
			speakername("Eevee?", PORTRAIT_DEMIVEE_ANNOYED, PORTRAIT_DEFAULT_LEFT)
			msgbox("You got tossed around unconscious in\n" "a flood! How'd you survive, anyway?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I'm made of tough stuff. It takes\n" "more than a little water to…", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_AMELIA, Common_Movement_ExclamationMark)
			waitmovement(0)
			applymovement(LOCALID_AMELIA, Common_Movement_Delay48)
			waitmovement(0)
			removeobject(LOCALID_AMELIA)
			playse(SE_BIKE_HOP)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaJumpUp)
			waitmovement(0)
			speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
			msgbox("The flood! What happened to my team?", MSGBOX_AUTOCLOSE)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaLookAround)
			waitmovement(0)
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("My team…ugh, my head. I can't think.")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I have their Poké Balls here.\n" "Guess I should go find them.", MSGBOX_AUTOCLOSE)
			additem(ITEM_POKE_BALL)
			additem(ITEM_GREAT_BALL)
			additem(ITEM_ULTRA_BALL)
			additem(ITEM_NEST_BALL)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaStartLeaving)
			delay(30)
			speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Wait! You shouldn't go alone.", MSGBOX_AUTOCLOSE)
			playse(SE_BIKE_HOP)
			applymovement(LOCALID_DEMIVEE, EltheRoad_Movement_DemiveeFollow)
			waitmovement(0)
			speakername("Eevee?", PORTRAIT_DEMIVEE_ANNOYED, PORTRAIT_DEFAULT_LEFT)
			msgbox("The local Pokémon are always testy\n" "after a flood.\p" "It's dangerous for a human to go\n" "alone. Take me!")
			speakername("Amelia", PORTRAIT_AMELIA_SMUG)
			msgbox("I can handle myself pretty well.\n" "And I'm a Pokémon Ranger, too.")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("But if you want to come along,\n" "I won't say no. Welcome aboard!", MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_DEMIVEE, EltheRoad_Movement_DemiveeJoin)
			waitmovement(0)
			removeobject(LOCALID_DEMIVEE)
			playfanfare(MUS_CAUGHT)
			additem(ITEM_LEAFSHARD)
			message("Eevee joined your party!")
			waitmessage
			waitfanfare
			waitbuttonpress
			closemessage
			fadedefaultbgm
			setvar(VAR_MAIN_GAME_STATE, 1)
		}
	]
}	

script EltheRoad_EventScript_FirstTrigger
{
	lock
	delay(30)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	applymovement(OBJ_EVENT_ID_FOLLOWER, Common_Movement_FaceRight)
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("I meant to ask earlier, but, uh,\n" "what are you, exactly?\p" "You look like an Eevee, but those\n" "markings…are they natural?")
	speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Uh, I dunno! I used to be normal,\n" "but I like to eat these crystals…\l" "I think I got these markings after\l" "I started doing that.\p"
			"Now when I touch an evolution stone,\n" "I don't evolve, but…\p"
			"It changes my type, I think.\n" "And my moves.")
	speakername("Amelia", PORTRAIT_AMELIA_SMUG)
	msgbox("So you're some kind of…Demi-Vee?")
	setpartymemberinfo(0) // Sets nickname, metloc, and Poke ball.
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_HUH, PORTRAIT_DEFAULT_LEFT)
	msgbox("Huh?")
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Um, anyway! If you find any cool\n" "crystals, give them to me!\l" "They'll make me stronger in battle.\p"
			"And if you need any advice for\n" "getting around, just ask me!\l" "I know this place pretty well!")
	speakername(NULL, 0)
	msgbox("(Press R or talk to Demi-Vee while\n" "he's following you to get advice!)\p" "(He will talk about the person\n" "you're facing, or give general hints\l" "for how to progress.)", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 2)
}

script EltheRoad_EventScript_FirstSign
{
	msgbox("{CLEAR_TO 60}{LEFT_ARROW} To Rehocan Town\n" "{CLEAR_TO 76}To Elthe City {RIGHT_ARROW}", MSGBOX_SIGN)
}

script EltheRoad_Trigger_VinosTrigger 
{
	lock
	speakername("Vine Mon")
	msgbox("Ugh…stupid flood.\n" "Washed away a perfect lurking spot.")
	speakername(NULL, 0)
	message("The Pokémon hasn't noticed you…\n" "What will you do?")
	waitmessage
	dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Call Out", "Step Forward", "Attack")
	closemessage
	switch(var(VAR_RESULT))
	{
		case 0:
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Excuse me…", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_VINOS, Common_Movement_FaceDown)
			waitmovement(0)
			applymovement(LOCALID_VINOS, Common_Movement_ExclamationMark)
			waitmovement(0)
			delay(48)
			speakername("Vine Mon")
			msgbox("A human…! I need to get in position!", MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_VINOS, EltheRoad_Movement_VinosGetOnLog)
			waitmovement(0)
			message("Now what?")
			waitmessage
			dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Negotiate", "Step Forward", "Attack")
			closemessage
			switch(var(VAR_RESULT))
			{
				case 0:
					speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
					msgbox("Do you mind if I pass through here?", MSGBOX_AUTOCLOSE)
					playse(SE_PIN)
					applymovement(LOCALID_VINOS, Common_Movement_ExclamationMark)
					waitmovement(0)
					delay(48)
					goto(EltheRoad_VinosNegotiationMain)
				case 1:
				// It jumps Amelia as soon as she steps forward. Do battle with +1 def/+2 speed on Vinos
				case 2:
				// She attacks and it springs forward. Do battle with +1 speed on vinos
			}
		case 1:
			applymovement(OBJ_EVENT_ID_FOLLOWER, Common_Movement_WalkUp)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
			waitmovement(0)
			delay(30)
			message("Now what?")
			waitmessage
			dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Call Out", "Step Forward", "Attack")
			closemessage
			switch(var(VAR_RESULT))
			{
				case 0:
				// It realizes it's on the back foot and opens up for negotiations. Easy negotiation ensues. If you talk to it from the left, you start here 
				case 1:
				// It gets startled by you being right behind it and flees.
				case 2:
				// Do battle with +1 atk on Demi-Vee
			}
		case 2:
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("Demi-Vee, attack!", MSGBOX_AUTOCLOSE)
			goto(EltheRoad_VinosDoBattle)			
	}
	
	// Negotiation flow
	//  - Give cheri berries: obtain 2 rock candy
	//  - Successful negotiation: obtain 2 rock candy
	//  - Passable negotiation: obtain 1 rock candy
	//  - Failed negotiation: Battle Vinos with a disadvantage.
	// - Negotiation flow: mid is +1 point. success is +2 points: fail is -1 point. You fail if you reach 0 points and pass if you reach 3.
	//  -- exhausting the questions without reaching 3 counts as a passable negotiation, as does reaching 3 points after a fail.
	//  -- some questions will be an instant failure state if you get them wrong.
	// - Question: What's your favorite type of Pokemon? (Grass: instant Fail, Dark: Mid, Fire: Success)
	// fuck grass types. i hate em. - amelia
}

// Does a random negotiation topic
script EltheRoad_VinosNegotiationMain
{
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	message("Alright, then…")
	waitmessage
	waitbuttonpress
	setvar(VAR_0x8008, 2)
	special(GetNegotiationTopic) // this sets temp flags
	if (var(VAR_RESULT) == 0xFF) // all topics used up
	{
		goto(EltheRoad_VinosNegotiationMidSuccess)
	}
	else
	{
		switch(var(VAR_RESULT))
		{
			case 0:
				dynmultipush("Dark", 1)
				dynmultipush("Grass", 0)
				dynmultipush("Fire", 2)
				message("Which Type of Pokémon is best,\n" "in your opinion?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, TRUE, 0, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
						msgbox("It's, uh…Grass. Yeah. I just love\n" "Grass type Pokémon…\p" "… … … … …")
						speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
						msgbox("You know what, no, I can't even f{CENSOR}uck{UNCENSOR}ing\n" "joke about it. All the major\l"
						"terrible events in my life were\l" "caused by Grass types.\l" "F{CENSOR}uck{UNCENSOR} Grass types. I hate 'em.")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("You'd disrespect me to my face…\n" "You have some nerve, human.", MSGBOX_AUTOCLOSE)
						setflag(FLAG_NOT_PERFECT_CONVERSATION)
						if (var(VAR_NEGOTIATION_SCORE) > 0)
						{
							subvar(VAR_NEGOTIATION_SCORE, 1)
						}
						playse(SE_FAILURE)
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Oh, Dark is the best, obviously!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Hmmhmm, yes, we are quite…\n" "Wait! You're just buttering me up!")
						speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
						msgbox("No way! I'm being honest! Some of my\n" "best friends are Dark types!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("…Well, alright. It's a good answer.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Hmm…I think it must be Fire.\n" "It's just such a cool element.")
						speakername("Amelia", PORTRAIT_AMELIA_WINK)
						msgbox("Plus, I was a Fire type for a while,\n" "myself. Self love, yo!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("You have some nerve, telling a Grass\n" "type that Fire is best…\p"
								"But I appreciate your honesty.\n" "The time for tricks has passed.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
			case 1:
	// - Question: Eat one of my berries! (Accept: Fail, Question logistics: Mid, Eat trail mix: Success)
				dynmultipush("Your berries?", 1)
				dynmultipush("Eat trail mix", 2)
				dynmultipush("Just eat it", 0)
				message("Would you like to eat\n" "one of my berries?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, TRUE, 0, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						callnative(ParalyzeLead)
						speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL)
						msgbox("Ooh, I'll take one!", MSGBOX_AUTOCLOSE)
						delay(90)
						speakername("Demi-Vee", PORTRAIT_DEMIVEE_ANNOYED)
						msgbox("Urp…")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Ha! What an idiot! You didn't even\n" "fall for my camouflage, but you\l"
								"still ate my paralyzing berries!\l" "You're mine!", MSGBOX_AUTOCLOSE)
						call(EltheRoad_CleanUpNegotiationValues)
						goto(EltheRoad_VinosDoBattle)
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Berries? They grow on your body?\n" "And you…WANT us to eat them?")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Well…yes. They're delicious.\n" "Or so I'm told.")
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("What about the ones that your eyes\n" "are on? Are they edible?")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("No, they're…my eyes. Not berries.")
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Has anyone ever tried to pick your\n" "eyes instead of a berry?")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("Look, are you gonna eat a berry\n" "or not!?")
						speakername("Amelia", PORTRAIT_AMELIA_SMUG)
						msgbox("Nope.")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("… … … … … … … … … …\p" "Okay, forget it. Next question.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Oh, that reminds me I have…", MSGBOX_AUTOCLOSE)
						// setvar(VAR_0x8004, ANIM_REACH_IN_POCKET)
						// callnative(DoAmeliaOverworldAnim)
						speakername("Amelia", PORTRAIT_AMELIA_MUNCH)
						msgbox("Trail mix!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("… … … … …\p" "Your ways confuse me, human.\n" "I would like to stop talking to you.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
				
			case 2:
	// - Question: Why do you need to get through? (To get a bath: Fail, Cuz I don't want to fight you: Mid, For thrills: Success)
				dynmultipush("To get my team back", 0)
				dynmultipush("For thrills", 2)
				dynmultipush("I don't want to fight", 1)
				message("Why should I let you through?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, TRUE, 0, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("I need to get my team back! We got\n" "separated by the flood.\p" "I'm their Trainer.\n" "We need each other.")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("Why is that my problem?\n" "I'm hungry.")
						speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
						msgbox("Hungr- were you planning to eat me!?")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("No. I was just giving an example of\n" "something that's not your problem.", MSGBOX_AUTOCLOSE)
						setflag(FLAG_NOT_PERFECT_CONVERSATION)
						if (var(VAR_NEGOTIATION_SCORE) > 0)
						{
							subvar(VAR_NEGOTIATION_SCORE, 1)
						}					
						playse(SE_FAILURE)	
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("There's no reason for us to fight.\n" "Look at this place, it's a mess.\p"
								"Just let me move on to take care of\n" "my business, and you can get back\l"
								"to finding a new tree, or whatever\l" "it is you're doing.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Pokémon don't need a reason to\n" "fight…but now that you mention it,\l"
								"it does sound like a waste\l" "of time right now…", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_SMUG)
						msgbox("I bet you fight every Trainer who\n" "comes through here, right?\p"
								"Wouldn't it be a thrill to let\n" "one pass through, just this once?\l"
								"Get a little crazy with it.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Hmm…perhaps under the circumstances,\n" "it would serve me better…", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
			case 3:
	// - Question: How long have you been training Pokemon? (30 years: mid, just started: fail, long enough: success)
				dynmultipush("Thirty years", 1)
				dynmultipush("Long enough", 2)
				dynmultipush("Just started", 0)
				message("How long has it been since you\n" "started training Pokémon?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, TRUE, 0, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Hmm, well me and Demi-Vee just met,\n" "so I guess I just started.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("No wonder you tried talking. You\n" "have no idea what you're doing.", MSGBOX_AUTOCLOSE)
						setflag(FLAG_NOT_PERFECT_CONVERSATION)
						if (var(VAR_NEGOTIATION_SCORE) > 0)
						{
							subvar(VAR_NEGOTIATION_SCORE, 1)
						}
						playse(SE_FAILURE)
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Let me think…Python's last birthday\n" "was, uh…\p"
								"Hmm, I can't remember exactly, but\n" "I think I started in the year…\p"
								"So, it must have been at least\n" "thirty years.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("You expect me to believe that?\n" "You don't even look like you're\l"
								"thirty years old. You're just\l" "trying to intimidate me.")
						speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
						msgbox("It's true, though…", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_SMUG)
						msgbox("Long enough.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("…!", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
		}
		
		delay(16)
		if (var(VAR_NEGOTIATION_SCORE) > 2)
		{
			if(flag(FLAG_NOT_PERFECT_CONVERSATION))
			{
				goto(EltheRoad_VinosNegotiationMidSuccess)
			}
			goto(EltheRoad_VinosNegotiationCompleteSuccess)
		}
		elif(var(VAR_NEGOTIATION_SCORE) == 0)
		{
			speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
			msgbox("You're getting on my nerves…\p" "Enough talk! I should have done this\n" "from the beginning!", MSGBOX_AUTOCLOSE)
			goto(EltheRoad_VinosDoBattle)
		}
		else
		{
			goto(EltheRoad_VinosNegotiationMain)
		}
	}
}

script EltheRoad_VinosNegotiationMidSuccess
{
	call(EltheRoad_CleanUpNegotiationValues)
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("…well, I suppose you're not wholly\n" "unpleasant. And I don't feel like\l"
			"jumping you anymore anyway.\l" "You can pass.\p"
			"And here, take this. I have no\n" "use for it anyway.", MSGBOX_AUTOCLOSE)
	giveitem(ITEM_EXP_CANDY_L, 2)
	// vinos leaves
}

script EltheRoad_VinosNegotiationCompleteSuccess
{
	call(EltheRoad_CleanUpNegotiationValues)
}

script EltheRoad_VinosDoBattle
{
	setwildbattle(SPECIES_BELLSPROUT, 35, ITEM_NONE)
	dowildbattle
	call(EltheRoad_CleanUpNegotiationValues)
	turnobject(LOCALID_VINOS, DIR_SOUTH)
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("… … … … …\p" "…well? Aren't you going to\n" "finish me off?")
	speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
	msgbox("What? No. I only wanted to get\n" "through here.")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("…Oh.", MSGBOX_AUTOCLOSE)
	delay(60)
	applymovement(LOCALID_VINOS, Common_Movement_FaceLeft)
	delay(120)
	applymovement(LOCALID_VINOS, Common_Movement_FaceRight)
	delay(120)
	applymovement(LOCALID_VINOS, Common_Movement_FaceDown)
	speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
	msgbox("Well, go along then! And if it\n" "will get you to leave me alone,\l" "then take this!", MSGBOX_AUTOCLOSE)
	giveitem(ITEM_EXP_CANDY_L)
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_YAY)
	msgbox("Ooh! A crystal! Yummy!")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Those things are all over the\n" "place here. I think I saw another\l" "type of crystal next to that stump\l" "behind me.\p" "Run along, then.", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 3)
	setflag(FLAG_TEMP_9) // Vinos's dialogue changes if you leave and come back
}

script EltheRoad_CleanUpNegotiationValues
{
	setvar(VAR_NEGOTIATION_SCORE, 0)
	clearflag(FLAG_TEMP_4)
	clearflag(FLAG_TEMP_1)
	clearflag(FLAG_TEMP_2)
	clearflag(FLAG_TEMP_3)
	clearflag(FLAG_NOT_PERFECT_CONVERSATION)
	return
}

script EltheRoad_EventScript_VinosIdle
{
	if(flag(FLAG_TEMP_9))
	{
		speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
		msgbox("If you truly just wanted to pass\n" "by, then hurry up already!\p"
				"I don't want to see your face\n" "right now. Begone.", MSGBOX_NPC)
	}
	else
	{
		speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
		msgbox("Normally, I try to disguise myself\n" "as a grapevine.\p"
				"If anyone is foolish enough to eat\n" "of my fruit, they'll be paralyzed\l" "soon enough to become my prey.\p"
				"It's just poor fortune that I've\n" "been left high and dry, so to speak,\l" "by this unbearable downpour.\p"
				"I can't even recall where my\n" "usual lurking spot is.", MSGBOX_NPC)
	}
}

movement EltheRoad_Movement_AmeliaJumpUp
{
	set_visible
	jump_in_place_left
}

movement EltheRoad_Movement_AmeliaLookAround
{
	face_up
	delay_16
	face_down
	delay_16
	face_up
	delay_16
	face_down
	delay_16
	face_right
	delay_16 * 3
	face_left
	delay_16 * 3
}

movement EltheRoad_Movement_AmeliaStartLeaving
{
	walk_up * 2
	walk_right * 3
	delay_16 * 2
	face_left
}

movement EltheRoad_Movement_DemiveeFollow
{
	jump_up
	walk_fast_up
	walk_fast_right * 2
}

movement EltheRoad_Movement_DemiveeJoin
{
	walk_slow_right
	enter_pokeball
}

movement EltheRoad_Movement_VinosGetOnLog
{
	walk_fast_right
	jump_right
	face_down
}
